emulate -LR zsh
setopt extendedglob warncreateglobal typesetsilent noshortloops

if [[ "$1" == plugin ]]
then
  local type="$1" user="$2" plugin="$3" id_as="$4" dir="${5#%}" hook="$6"
else
  local type="$1" url="$2" id_as="$3" dir="${4#%}" hook="$5"
fi

local name="z-a-python"

.py-install() {
  local tool="$1" repo="$2"

  if [[ -z "$repo" ]]
  then
    +zi-log "{annex}${name}{rst} {e}Empty repo value"
    return 1
  fi

  local -a cmd
  case "$tool" in
    uv)
      cmd=(uv tool upgrade)
      ;;
    pip)
      cmd=(pip install --user --upgrade)
      ;;
    pipx)
      cmd=(pipx upgrade)
      ;;
    +)
      +zi-log "{e} Unknown tool {file}${tool}{rst}"
      return 1
      ;;
  esac

  +zi-log "{m} {b}{annex}${name}{rst}: $cmd {file}$repo{rst}"
  $=cmd $repo
}

local i val
local -a values
for i in uv pip pipx
do
  if (( ${+ICE[$i]} ))
  then
    builtin cd -q "$dir" || return

    values=(${(s.;.)ICE[$i]})

    for val in $values
    do
      val=${val:-$PWD}
      .py-install "$i" "$val"
    done
  fi
done

# vim: set ft=zsh et ts=2 sw=2 :
