emulate -LR zsh
setopt extendedglob warncreateglobal typesetsilent noshortloops

autoload za-py-magic

if [[ "$1" == plugin ]]
then
  local type="$1" user="$2" plugin="$3" id_as="$4" dir="${5#%}" hook="$6"
else
  local type="$1" url="$2" id_as="$3" dir="${4#%}" hook="$5"
fi

local name="za-py-handler"

local action
case "$hook" in
  *atclone*)
    action=install
    ;;
  *atpull*)
    action=update
    ;;
  *delet*)
    action=delete
    ;;
  *)
    +zi-log "{annex}${name}{rst} {e} unknown hook: {file}$hook{rst}"
    return 2
    ;;
esac

local i val pypkg
for i in uv pip pipx
do
  if (( ${+ICE[$i]} ))
  then
    for val in "${ICE[$i]}"
    do
      pypkg=${val:-${dir}}
      za-py-magic "$i" "$action" "$pypkg" "$dir"
    done
  fi
done

# vim: set ft=zsh et ts=2 sw=2 :
